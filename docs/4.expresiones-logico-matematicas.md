```
Universidad del Quindío
Programa de Ingeniería de Sistemas y Computación
Programación III - Expresiones lógico-matemáticas
```

# Expresiones lógico-matemáticas

Para comprender las expresiones lógico-matemáticas en Elixir, es importante familiarizarse con su sintaxis y características principales. A continuación, se presentan algunos de los conceptos fundamentales que se abordarán en esta guía:

- Operadores lógicos y matemáticos.
- Funciones lambda.
- Evaluación de expresiones.
- Patrones de coincidencia (pattern matching).
- Ejemplos prácticos.

## Ejercicio 1

La oﬁcina de tesorería de una empresa requiere de un algoritmo que le permita calcular el salario a pagar a un empleado. Al empleado le pagan de acuerdo con el número de horas que haya laborado durante el periodo a razón de un valor específico cada hora. Calcule el salario de un empleado, pidiendo su nombre, horas trabajadas y valor por hora.

```elixir
defmodule Salario do

  def main do
    nombre = leer_nombre()
    horas_trabajadas = leer_horas_trabajadas()
    valor_por_hora = leer_valor_por_hora()
    salario = calcular_salario(horas_trabajadas, valor_por_hora)
    imprimir_mensaje(nombre, salario)
  end

  def leer_nombre do
    IO.gets("Ingrese el nombre del empleado:") 
    |> String.trim()
  end

  def leer_horas_trabajadas do
    IO.gets("Ingrese las horas trabajadas:") 
    |> String.trim() 
    |> String.to_integer()
  end

  def leer_valor_por_hora do
    IO.gets("Ingrese el valor por hora:") 
    |> String.trim() 
    |> String.to_float()
  end

  def calcular_salario(horas_trabajadas, valor_por_hora) do
    horas_trabajadas * valor_por_hora
  end

  def imprimir_mensaje(nombre, salario) do
    IO.puts("El salario de #{nombre} es: $#{salario}")
  end
end

Salario.main()
```

El módulo `String` de elixir tiene una serie de funciones útiles para manipular cadenas de texto. Algunas de estas funciones incluyen:

- `String.trim/1`: Elimina los espacios en blanco al inicio y al final de una cadena.
- `String.to_integer/1`: Convierte una cadena en un número entero.
- `String.to_float/1`: Convierte una cadena en un número de punto flotante.
- `String.length/1`: Devuelve la longitud de una cadena.
- `String.at/2`: Devuelve el carácter en la posición especificada de una cadena.

> **⚠️ Importante:** Las funciones `String.to_integer/1` y `String.to_float/1` pueden fallar si la cadena no representa un número válido. Para manejar estos casos, se recomienda utilizar estas funciones dentro de un bloque `try`/`catch` o utilizarlas junto con `case` para proporcionar un valor predeterminado en caso de error.


```elixir
defmodule Salario do

  def main do
    nombre = leer_nombre()
    horas_trabajadas = leer_horas_trabajadas()
    valor_por_hora = leer_valor_por_hora()
    salario = calcular_salario(horas_trabajadas, valor_por_hora)
    imprimir_mensaje(nombre, salario)
  end

  def leer_nombre do
    IO.gets("Ingrese el nombre del empleado:") 
    |> String.trim()
  end

  def leer_horas_trabajadas do
    horas = IO.gets("Ingrese las horas trabajadas:") 
    |> String.trim() 
    |> Integer.parse()

    case horas do
      {numero, _} -> numero
      :error -> 
        imprimir_error("Horas trabajadas inválidas. Se utilizará 0 como valor predeterminado.")
        0
    end
  end

  def leer_valor_por_hora do
    valor = IO.gets("Ingrese el valor por hora:") 
    |> String.trim() 
    |> Float.parse()

    case valor do
      {numero, _} -> numero
      :error -> 
        imprimir_error("Valor por hora inválido. Se utilizará 0 como valor predeterminado.")
        0
    end
  end

  def calcular_salario(horas_trabajadas, valor_por_hora) do
    horas_trabajadas * valor_por_hora
  end

  def imprimir_mensaje(nombre, salario) do
    IO.puts("El salario de #{nombre} es: $#{salario}")
  end

  def imprimir_error(mensaje) do
    IO.puts(:standard_error, mensaje)
  end

end

Salario.main()
```

En este caso se opta por usar `Integer.parse()` y `Float.parse()` para manejar la entrada del usuario de manera más robusta, ya que permiten capturar errores de conversión y proporcionar valores predeterminados en caso de entradas inválidas. 

Ambas funciones retornan un `tupla` con el número parseado y el resto de la cadena. Si no se puede parsear, retornan `:error`. 

## Ejercicio 2

Se requiere un programa que calcule varias operaciones aritméticas a partir de dos números ingresados por el usuario

```elixir
defmodule Ejercicio1 do
  def evaluar_expresion(expr) do
    case expr do
      {:suma, a, b} -> evaluar_expresion(a) + evaluar_expresion(b)
      {:resta, a, b} -> evaluar_expresion(a) - evaluar_expresion(b)
      {:multiplicacion, a, b} -> evaluar_expresion(a) * evaluar_expresion(b)
      {:division, a, b} -> evaluar_expresion(a) / evaluar_expresion(b)
      valor -> valor
    end
  end
end
```

